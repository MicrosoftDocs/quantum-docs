---
author: cgranade
ms.author: chgranad
ms.date: 06/09/2022
ms.service: azure-quantum
ms.subservice: core
ms.topic: conceptual
no-loc: ['Q#', '$$v', '$$', "$$", '$', "$", $, $$, '\cdots', 'bmatrix', '\ddots', '\equiv', '\sum', '\begin', '\end', '\sqrt', '\otimes', '{', '}', '\text', '\phi', '\kappa', '\psi', '\alpha', '\beta', '\gamma', '\delta', '\omega', '\bra', '\ket', '\boldone', '\\\\', '\\', '=', '\frac', '\text', '\mapsto', '\dagger', '\to', '\begin{cases}', '\end{cases}', '\operatorname', '\braket', '\id', '\expect', '\defeq', '\variance', '\dd', '&', '\begin{align}', '\end{align}', '\Lambda', '\lambda', '\Omega', '\mathrm', '\left', '\right', '\qquad', '\times', '\big', '\langle', '\rangle', '\bigg', '\Big', '|', '\mathbb', '\vec', '\in', '\texttt', '\ne', '<', '>', '\leq', '\geq', '~~', '~', '\begin{bmatrix}', '\end{bmatrix}', '\_']
title: Q# compiler message QS6307
uid: microsoft.quantum.qscompiler-diagnostics.QS6307
---

# QS6307

This compiler error is raised when a function or operation is declared as returning a value, but does not include either a `return` or `fail` statement for each possible code path.

```qsharp
// Causes error QS6307.
function Plus(x : Int, y : Int) : Int {
    let z = x + y;
}
```

This compiler error can be resolved by either returning a value or failing in each code path, or by changing the return type to `Unit`.

## Error text

Not all code paths return a value.

## Details

This error is caused when a function or operation is declared as returning a value, but does not do so in all possible code paths. For example, if a function or operation includes an `if`/`else` block, there must either be a `return` or `fail` statement that applies whether the condition for the block is `true` or `false`.

```qsharp
// Causes error QS6307.
function Plus(x : Int, y : Int) : Int {
                               // ^^^
                               // |
                               // The return type is declared as Int here.
    let z = x + y;
    // This code path does not return a value, but a value of type Int
    // is expected.
}
```

In the special case that a function or operation has a return type of `Unit`, then `return ();` is implied in all code paths that do not explicitly fail or return a value.

```qsharp
function PlusMessage(x : Int, y : Int) : Unit {
                                      // ^^^^
                                      // |
                                      // The return type is declared as
                                      // Unit, such that return statements are
                                      // optional.
    let z = x + y;
    Message($"{z}");
    // No return statement is included along this code path, so return (); is
    // implied.
}
```

## Suggestions

To resolve this error, you may either explicitly return a value or fail for each code path. For example, consider a function that returns `x + y` if `x + y` is even, and that is intended to fail otherwise. If an explicit `fail` statement is omitted, then QS6307 will be raised as a result:

```qsharp
// Causes error QS6307.
function PlusWhenEven(x : Int, y : Int) : Int {
    let z = x + y;
    if z % 2 == 0 {
        return z;
    }
    // QS6307 raised here, as this code path is missing either a return or fail
    // statement.
}
```

To fix this, you may want to explicitly fail when control flow bypasses the
conditional block:

```qsharp
function PlusWhenEven(x : Int, y : Int) : Int {
    let z = x + y;
    if z % 2 == 0 {
        return z;
    }
    fail "Expected x + y to be even.";
}
```

Alternatively, if no return value is intended, you may be able to resolve this error by changing the return type to be `Unit`. For example:

```qsharp
// Causes error QS6307.
operation ApplySqrtX(target : Qubit) : Result {
                                    // ^^^^^^
                                    // |
                                    // Declares the return type for this
                                    // operation to be a measurement result,
                                    // which is likely not correct for unitary
                                    // operations.
    within {
        H(target);
    } apply {
        S(target);
    }
    // QS6307 raised here, since this code path doesn't return a measurement
    // result.
}
```

Changing the return type to `Unit` resolves the error in this case:

```qsharp
operation ApplySqrtX(target : Qubit) : Unit is Adj + Ctl {
                                    // ^^^^^^^^^^^^^^^^^
                                    // |
                                    // This declaration specifies that
                                    // ApplySqrtX returns the unit value (),
                                    // and is both adjointable and
                                    // controllable.
    within {
        H(target);
    } apply {
        S(target);
    }
}
```
